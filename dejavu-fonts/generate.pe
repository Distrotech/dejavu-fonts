#!/usr/bin/env fontforge
# $Id$

# script file for FontForge for TTF generation
# usage:
#   mkdir generated
#   chmod +x generate.pe
#   ./generate.pe *.sfd

# font generation flags:
#      8 => do not include TT instructions (for experimental typefaces)
#   0x20 => generate a 'PfEd' table and store glyph comments
#   0x40 => generate a 'PfEd' table and store glyph colors
#   0x80 => generate tables so the font will work on both Apple and MS platforms.
#           - this one is important because it generates correct kerning tables
def_gen_flags = 0x20 + 0x40 + 0x80
exp_gen_flags = def_gen_flags + 8

if ($version < "20050502")
  Error("Your version of FontForge messes with morx TT table");
endif
# FoundryName is not used in TTF generation
SetPref("FoundryName", "DejaVu")
# first 4 characters of TTFFoundry are used for achVendId
SetPref("TTFFoundry", "DejaVu")
i = 1
while ( i < $argc )
  Open($argv[i], 1)
  gen_flags = def_gen_flags
  # Serif Oblique and Serif Bold Oblique are experimental
  if ((Strcasestr ($fontname, "Serif") > -1) && (Strcasestr ($fontname, "Oblique") > -1))
    gen_flags = exp_gen_flags
  endif
  if (Strcasestr ($fontname, "Condensed") > -1)
    gen_flags = exp_gen_flags
  endif
  # no non-combining ligatures in Mono (we remove all because there are no valid combining ligatures right now)
  if (Strcasestr ($fontname, "Mono") > -1)
    SelectAll()
    RemoveATT("Ligature", "*", "*")
    SelectNone()
  endif
  # ONLY FOR 2.2 RELEASE: remove ligature tag from U+FB00, U+FB03 and U+FB04
  SelectSingletons(0uFB00, 0uFB03, 0uFB04)
  RemoveATT("Ligature", "*", "*")
  SelectNone()
  Generate("generated/" + $fontname + ".ttf", "", gen_flags)
  i++
endloop
